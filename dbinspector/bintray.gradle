apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            infinum(MavenPublication) {
                groupId = dbinspector.group
                artifactId = "dbinspector"
                version = dbinspector.version

                artifact bundleReleaseAar
                artifact javadocsJar
                artifact sourcesJar

                pom {
                    name = "Android DbInspector"
                    description = "Android library for viewing in app databases."
                    url = "https://github.com/infinum/android_dbinspector"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/infinum/android_dbinspector/blob/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "reisub"
                            name = "Dino Kovač"
                        }
                        developer {
                            id = "knobtviker"
                            name = "Bojan Komljenović"
                            email = "bojan.komljenovic@infinum.com"
                        }
                    }
                    scm {
                        url = "scm:git@github.com:infinum/android_dbinspector.git"
                        connection = "scm:git@github.com:infinum/android_dbinspector.git"
                        developerConnection = "scm:git@github.com:infinum/android_dbinspector.git"
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode("dependencies")
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", it.group)
                        dependencyNode.appendNode("artifactId", it.name)
                        dependencyNode.appendNode("version", it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def bintrayProperties = rootProject.file("bintray.properties")
    if (bintrayProperties.exists()) {
        properties.load(new FileInputStream(bintrayProperties))
    } else {
        properties.setProperty("bintray.user", "")
        properties.setProperty("bintray.apikey", "")
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ["infinum"]

    pkg {
        repo = "android"
        name = "dbinspector"
        userOrg = "infinum"
        desc = "DbInspector package"
        websiteUrl = "https://github.com/infinum/android_dbinspector"
        issueTrackerUrl = "https://github.com/infinum/android_dbinspector/issues"
        vcsUrl = "https://github.com/infinum/android_dbinspector.git"
        licenses = ["Apache-2.0"]
        version {
            name = "${dbinspector.version}"
            released = new Date()
            vcsTag = "${dbinspector.version}"
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }
}
