import com.infinum.maven.GithubConfiguration
import com.infinum.maven.SonatypeConfiguration

apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                def configuration = new SonatypeConfiguration()
                configuration.load()
                name configuration.name()
                url configuration.url()
                credentials {
                    username configuration.username()
                    password configuration.password()
                }
            }
            maven {
                def configuration = new GithubConfiguration()
                configuration.load()
                name configuration.name()
                url configuration.url()
                credentials {
                    username configuration.username()
                    password configuration.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = dbinspector.group
                version = dbinspector.version

                artifactId = "dbinspector"

                artifact bundleReleaseAar
                artifact javadocsJar
                artifact sourcesJar

                pom {
                    name = "Android DbInspector"
                    description = "Android library for viewing in app databases."
                    url = "https://github.com/infinum/android_dbinspector"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/infinum/android_dbinspector/blob/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "reisub"
                            name = "Dino Kovač"
                        }
                        developer {
                            id = "knobtviker"
                            name = "Bojan Komljenović"
                            email = "bojan.komljenovic@infinum.com"
                        }
                    }
                    scm {
                        url = "scm:git@github.com:infinum/android_dbinspector.git"
                        connection = "scm:git@github.com:infinum/android_dbinspector.git"
                        developerConnection = "scm:git@github.com:infinum/android_dbinspector.git"
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode("dependencies")
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", it.group)
                        dependencyNode.appendNode("artifactId", it.name)
                        dependencyNode.appendNode("version", it.version)
                    }
                }
            }
        }
    }
}
