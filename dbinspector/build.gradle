plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "com.google.protobuf"
    id "kover"
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode releaseConfig.versionCode
        versionName releaseConfig.version
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    resourcePrefix "dbinspector_"

    lintOptions {
        disable 'RtlEnabled', 'VectorPath', 'IconLocation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-Xexplicit-api=strict'
        ]
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            kover {
                enabled = name == "testDebugUnitTest"
//                includes = ['com\\.infinum\\.dbinspector\\..*']
//                excludes = ['com.infinum.dbinspector.databinding']
//                includes = [
//                        '**/*ViewModel*.*',
//                        '**/*UseCase*.*',
//                        '**/*Repository*.*',
//                        '**/*Control*.*',
//                        '**/*Converter*.*',
//                        '**/*Mapper*.*',
//                        '**/*Interactor*.*',
//                        '**/data/sources**/**'
//                ]
//                excludes = [
//                        '**/base/Base*.*',
//                        '**/*Test*.*',
//                        '**/AutoValue_*.*',
//                        '**/*JavascriptBridge.class',
//                        '**/R.class',
//                        '**/R$*.class',
//                        '**/Manifest*.*',
//                        'android/**/*.*',
//                        '**/BuildConfig.*',
//                        '**/*$ViewBinder*.*',
//                        '**/*$ViewInjector*.*',
//                        '**/Lambda$*.class',
//                        '**/Lambda.class',
//                        '**/*Lambda.class',
//                        '**/*Lambda*.class',
//                        '**/lambda$*.class',
//                        '**/lambda.class',
//                        '**/*lambda.class',
//                        '**/*lambda*.class',
//                        '**/*$$special$$inlined$inject$*.*',
//                        '**/*$InjectAdapter.class',
//                        '**/*$ModuleAdapter.class',
//                        '**/*$ViewInjector*.class',
//                        '**/*$DefaultImpls*.*',
//                        '**/*_MembersInjector.class', //Dagger2 generated code
//                        '*/*_MembersInjector*.*', //Dagger2 generated code
//                        '**/*_*Factory*.*', //Dagger2 generated code
//                        '*/*Component*.*', //Dagger2 generated code
//                        '**/*Module*.*' //Dagger2 generated code
//                ]
            }
        }
    }

    sourceSets.each {
        it.proto.srcDirs += "src/$it.name/proto"
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.coroutines.android
    implementation libs.bundles.androidx
    implementation libs.protobuf.javalite
    implementation libs.material
    implementation libs.koin.android
    implementation libs.fuzzy

    testImplementation libs.bundles.test
}

protobuf {
    protoc {
        def protobufArtifact = libs.protobuf.artifact.get()
        artifact = protobufArtifact.getModule().toString() + ":" + protobufArtifact.getVersionConstraint().toString()
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

kover {
    enabled = true
    coverageEngine.set(kotlinx.kover.api.CoverageEngine.INTELLIJ)
    intellijEngineVersion.set('1.0.622')
    jacocoEngineVersion.set(libs.versions.jacoco.get())
    generateReportOnCheck.set(true)
}

tasks.withType(Test) {
    useJUnitPlatform()
}

apply from: "publish.gradle"
