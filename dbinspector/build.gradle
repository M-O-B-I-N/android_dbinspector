plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.google.protobuf"
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode releaseConfig.versionCode
        versionName releaseConfig.version
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    resourcePrefix "dbinspector_"

    lintOptions {
        disable 'RtlEnabled', 'VectorPath', 'IconLocation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-Xexplicit-api=strict'
        ]
        useIR = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets.each {
        it.proto.srcDirs += "src/$it.name/proto"
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.coroutines
    implementation libs.bundles.androidx
    implementation libs.protobuf.javalite
    implementation libs.material
    implementation libs.koin
    implementation libs.fuzzy

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"
    testImplementation "io.insert-koin:koin-test:3.0.1"
    testImplementation "io.insert-koin:koin-test-junit5:3.0.1"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "org.mockito:mockito-core:3.8.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    testImplementation 'org.mockito:mockito-inline:3.5.10'
    testImplementation "org.mockito:mockito-junit-jupiter:3.5.10"
}

protobuf {
    protoc {
        def protobufArtifact = libs.protobuf.artifact.get()
        artifact = protobufArtifact.getModule().toString() + ":" + protobufArtifact.getVersionConstraint().toString()
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

apply from: "jacoco.gradle"
apply from: "publish.gradle"
