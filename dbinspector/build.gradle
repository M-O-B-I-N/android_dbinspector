import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.api.tasks.testing.TestListener
import org.gradle.api.tasks.testing.TestResult.ResultType
import groovy.time.TimeCategory
import groovy.time.TimeDuration

plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "com.google.protobuf"
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode releaseConfig.versionCode
        versionName releaseConfig.version
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    resourcePrefix "dbinspector_"

    lintOptions {
        disable 'RtlEnabled', 'VectorPath', 'IconLocation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-Xexplicit-api=strict'
        ]
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets.each {
        it.proto.srcDirs += "src/$it.name/proto"
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.coroutines.android
    implementation libs.bundles.androidx
    implementation libs.protobuf.javalite
    implementation libs.material
    implementation libs.koin.android
    implementation libs.fuzzy

    testImplementation libs.bundles.test
}

protobuf {
    protoc {
        def protobufArtifact = libs.protobuf.artifact.get()
        artifact = protobufArtifact.getModule().toString() + ":" + protobufArtifact.getVersionConstraint().toString()
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
//    testLogging {
//        events TestLogEvent.FAILED, TestLogEvent.SKIPPED // , TestLogEvent.PASSED
//    }
    def testListener = new TestListener() {

        def template = """

# {timestamp}
## Duration
{duration}
## Count 
| Total         | Passed         | Failed         | Failed          |
|:-------------:|:--------------:|:--------------:|:---------------:|
| {total_count} | {passed_count} | {failed_count} | {skipped_count} |
## Details
<details>
  <summary>PASSED</summary>

{passed}
</details>
<details>
  <summary>FAILED</summary>
  
{failed}
</details>
<details>
  <summary>SKIPPED</summary>
  
{skipped}
</details>

"""
        ArrayList<TestDescriptor> passed = new ArrayList<TestDescriptor>()
        ArrayList<TestDescriptor> failed = new ArrayList<TestDescriptor>()
        ArrayList<TestDescriptor> skipped = new ArrayList<TestDescriptor>()

        @Override
        void beforeSuite(TestDescriptor testDescriptor) {

        }

        @Override
        void afterSuite(TestDescriptor testDescriptor, TestResult testResult) {
            if (testDescriptor.getParent() == null) {
                TimeDuration duration = TimeCategory.minus(new Date(testResult.endTime), new Date(testResult.startTime))

                def passedSuite = ""
                Map<TestDescriptor, List<TestDescriptor>> passedMap = passed.groupBy { it.parent }
                passedMap.each {
                    passedSuite += "  * " + it.key.displayName
                    passedSuite += "\n"
                    it.value.each {
                        passedSuite += "    * " + it.displayName
                        passedSuite += "\n"
                    }
                }
                def failedSuite = ""
                Map<TestDescriptor, List<TestDescriptor>> failedMap = failed.groupBy { it.parent }
                failedMap.each {
                    failedSuite += "  * " + it.key.displayName
                    failedSuite += "\n"
                    it.value.each {
                        failedSuite += "    * " + it.displayName
                        failedSuite += "\n"
                    }
                }
                def skippedSuite = ""
                Map<TestDescriptor, List<TestDescriptor>> skippedMap = skipped.groupBy { it.parent }
                skippedMap.each {
                    skippedSuite += "  * " + it.key.displayName
                    skippedSuite += "\n"
                    it.value.each {
                        skippedSuite += "    * " + it.displayName
                        skippedSuite += "\n"
                    }
                }

                File file = new File("test_report.md")
                file << template
                        .replace("{timestamp}", new Date().format("dd.MM.yyyy. - HH:mm"))
                        .replace("{duration}", duration.toString())
                        .replace("{total_count}", testResult.testCount + "")
                        .replace("{passed_count}", testResult.successfulTestCount + "")
                        .replace("{failed_count}", testResult.failedTestCount + "")
                        .replace("{skipped_count}", testResult.skippedTestCount + "")
                        .replace("{passed}", passedSuite)
                        .replace("{failed}", failedSuite)
                        .replace("{skipped}", skippedSuite)
            }
        }

        @Override
        void beforeTest(TestDescriptor testDescriptor) {

        }

        @Override
        void afterTest(TestDescriptor testDescriptor, TestResult testResult) {
//            println('\n\nNAME: ' + testDescriptor.name + '\nDISPLAY NAME: ' + testDescriptor.displayName + '\nCLASS NAME: ' + testDescriptor.className + '\nIS COMPOSITE: ' + testDescriptor.isComposite() + '\nPARENT: ' + testDescriptor.parent + '\nRESULT: ' + testResult.resultType)
            if (testResult.resultType == ResultType.FAILURE || testResult.resultType == ResultType.SKIPPED) {
                println('\n\nNAME: ' + testDescriptor.name + '\nDISPLAY NAME: ' + testDescriptor.displayName + '\nCLASS NAME: ' + testDescriptor.className + '\nIS COMPOSITE: ' + testDescriptor.isComposite() + '\nPARENT: ' + testDescriptor.parent + '\nRESULT: ' + testResult.resultType)
            }
            if (testResult.resultType == ResultType.FAILURE) {
                failed += testDescriptor
            } else if (testResult.resultType == ResultType.SKIPPED) {
                skipped += testDescriptor
            } else if (testResult.resultType == ResultType.SUCCESS) {
                passed += testDescriptor
            }
        }
    }
    addTestListener(testListener)
    finalizedBy jacocoTestReport
}

apply from: "jacoco.gradle"
apply from: "publish.gradle"
