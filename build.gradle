buildscript {

    ext.buildConfig = [
            "minSdk"    : 21,
            "compileSdk": 30,
            "targetSdk" : 30,

            "buildTools": "30.0.3"
    ]

    apply from: "dependencies.gradle"

    repositories {
        google()
        maven { url "https://dl.bintray.com/infinum/android" }
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.bintray
        classpath packages.kotlin.plugin
        classpath packages.cpd.plugin
        classpath packages.detekt.plugin
        classpath packages.ktlint.plugin
        classpath packages.dokka.plugin
        classpath packages.google.protobuf.plugin
    }
}

allprojects {

    repositories {
        google()
        maven { url "https://dl.bintray.com/infinum/android" }
        jcenter()
        maven { url "https://jitpack.io" }
    }

    def buildProperties = new Properties()
    file(rootDir.absolutePath + "/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.dbinspector.dbinspector) with project(':dbinspector')
                    substitute module(packages.dbinspector.dbinspector_no_op) with project(':dbinspector-no-op')
                }
            }
        }
    }
}

subprojects {
    repositories {
        google()
        maven { url "https://dl.bintray.com/infinum/android" }
        jcenter()
        mavenCentral()
    }

    apply from: "../cpd.gradle"
    apply from: "../detekt.gradle"
    apply from: "../ktlint.gradle"
    apply from: "../dokka.gradle"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':dbinspector:detekt',
        ':dbinspector-no-op:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

task runStaticChecks(dependsOn: ordered(
        ':detektAll',
        ':dbinspector:ktlintCheck',
        ':dbinspector-no-op:ktlintCheck',
        ':dbinspector:cpdCheck',
        ':dbinspector-no-op:cpdCheck'
)) {
    group = "Verification"
    description = "Run static checks on all modules"
}

apply from: "deploy.gradle"

def ordered(String... taskPaths) {
    def allTasks = taskPaths.collect { tasks.getByPath(it) }
    for (int i = 0; i < allTasks.size() - 1; i++) {
        allTasks[i + 1].mustRunAfter(allTasks[i])
    }
    return allTasks
}
