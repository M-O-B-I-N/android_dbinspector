buildscript {

    ext.buildConfig = [
            "minSdk"    : 21,
            "compileSdk": 30,
            "targetSdk" : 30,

            "buildTools": "30.0.3"
    ]

    apply from: "dependencies.gradle"

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/infinum/android_dbinspector"
            Properties properties = new Properties()
            def githubProperties = rootProject.file("github.properties")
            if (githubProperties.exists()) {
                properties.load(new FileInputStream(githubProperties))
            } else {
                properties.setProperty("github.user", "")
                properties.setProperty("github.token", "")
            }
            credentials {
                username = properties.getProperty("github.user")
                password = properties.getProperty("github.token")
            }
        }
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.kotlin.plugin
        classpath packages.cpd.plugin
        classpath packages.detekt.plugin
        classpath packages.ktlint.plugin
        classpath packages.dokka.plugin
        classpath packages.google.protobuf.plugin
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/infinum/android_dbinspector"
            Properties properties = new Properties()
            def githubProperties = rootProject.file("github.properties")
            if (githubProperties.exists()) {
                properties.load(new FileInputStream(githubProperties))
            } else {
                properties.setProperty("github.user", "")
                properties.setProperty("github.token", "")
            }
            credentials {
                username = properties.getProperty("github.user")
                password = properties.getProperty("github.token")
            }
        }
        jcenter()
    }

    def buildProperties = new Properties()
    file(rootDir.absolutePath + "/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.dbinspector.dbinspector) with project(':dbinspector')
                    substitute module(packages.dbinspector.dbinspector_no_op) with project(':dbinspector-no-op')
                }
            }
        }
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/infinum/android_dbinspector"
            Properties properties = new Properties()
            def githubProperties = rootProject.file("github.properties")
            if (githubProperties.exists()) {
                properties.load(new FileInputStream(githubProperties))
            } else {
                properties.setProperty("github.user", "")
                properties.setProperty("github.token", "")
            }
            credentials {
                username = properties.getProperty("github.user")
                password = properties.getProperty("github.token")
            }
        }
        jcenter()
    }

    apply from: "../cpd.gradle"
    apply from: "../detekt.gradle"
    apply from: "../ktlint.gradle"
    apply from: "../dokka.gradle"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':dbinspector:detekt',
        ':dbinspector-no-op:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':dbinspector:ktlintCheck',
        ':dbinspector-no-op:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktlintCheck on all modules"
}

task cpdAll(dependsOn: [
        ':dbinspector:cpdCheck',
        ':dbinspector-no-op:cpdCheck'
]) {
    group = "Verification"
    description = "Run cpdCheck on all modules"
}

apply from: "shared.gradle"

task runStaticChecks(dependsOn: ordered(
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
)) {
    group = "Verification"
    description = "Run static checks on all modules"
}

apply from: "deploy.gradle"
