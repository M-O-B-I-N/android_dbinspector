buildscript {

    ext.buildConfig = [
            "minSdk"    : 21,
            "compileSdk": 30,
            "targetSdk" : 30,

            "buildTools": "30.0.3"
    ]

    apply from: "maven.gradle"
    apply from: "dependencies.gradle"

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.kotlin.plugin
        classpath packages.cpd.plugin
        classpath packages.detekt.plugin
        classpath packages.ktlint.plugin
        classpath packages.dokka.plugin
        classpath packages.google.protobuf.plugin
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        jcenter() {
            content {
                includeModule("com.infinum.dbinspector", "dbinspector")
                includeModule("com.infinum.dbinspector", "dbinspector-no-op")
                includeModule("com.soywiz.korlibs.korte", "korte-jvm")
                includeModule("org.jetbrains.trove4j", "trove4j")
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
                includeGroup("org.jetbrains.dokka")
                includeGroup("org.jetbrains")
                includeGroup("org.koin")
            }
        }
    }

    def buildProperties = new Properties()
    file(rootDir.absolutePath + "/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.dbinspector.dbinspector) with project(':dbinspector')
                    substitute module(packages.dbinspector.dbinspector_no_op) with project(':dbinspector-no-op')
                }
            }
        }
    }
}

subprojects {
    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

apply from: "shared.gradle"
apply from: "deploy.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':dbinspector:detekt',
        ':dbinspector-no-op:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':dbinspector:ktlintCheck',
        ':dbinspector-no-op:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktlintCheck on all modules"
}

task cpdAll(dependsOn: [
        ':dbinspector:cpdCheck',
        ':dbinspector-no-op:cpdCheck'
]) {
    group = "Verification"
    description = "Run cpdCheck on all modules"
}

task runStaticChecks(dependsOn: ordered(
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
)) {
    group = "Verification"
    description = "Run static checks on all modules"
}
